apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def libName = 'lastadapter'
def libGroup = 'com.github.nitrico.' + libName
def libSite = 'https://github.com/nitrico/' + libName
def libGit = libSite + '.git'
def libTracker = libSite + '/issues'
def libDesc = "Don't write any other RecyclerView adapter again. Not even a ViewHolder!"
def libTags = ['android', 'recyclerview', 'adapter', 'data binding', 'kotlin']

group libGroup
version '1.2.1'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    configurations = ['archives']
    user properties.getProperty('bintray.user')
    key properties.getProperty('bintray.key')
    pkg {
        name = libName
        repo = 'maven'
        desc = libDesc
        websiteUrl = libSite
        issueTrackerUrl = libTracker
        vcsUrl = libGit
        licenses = ['Apache-2.0']
        labels = libTags
        publish = true
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            name = libName
            description = libDesc
            packaging 'aar'
            groupId libGroup
            artifactId libName
            url libSite

            licenses {
                license {
                    name = 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    name = 'Miguel √Ångel Moreno'
                    id 'moreno'
                    email 'nitrico@gmail.com'
                }
            }
            scm {
                connection libGit
                developerConnection libGit
                url libSite
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

artifacts {
    archives sourcesJar
}
